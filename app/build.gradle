apply plugin: 'com.android.application'

// http://stackoverflow.com/a/35041457/513038
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.erhannis.unstableart"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
}

repositories {
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-snapshots/"
    }
    mavenLocal()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //TODO I wish I didn't have to use streamsupport!
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.erhannis:MathNStuff:0.1.0-SNAPSHOT'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.esotericsoftware.kryo:kryo:2.23-SNAPSHOT'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.5.4'
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.5.4'
    implementation 'net.sourceforge.streamsupport:streamsupport-atomic:1.5.4'
    implementation 'net.sourceforge.streamsupport:streamsupport-flow:1.5.4'
    implementation 'net.sourceforge.streamsupport:streamsupport-literal:1.5.4'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.github.danielnilsson9:color-picker-view:1.4.0@aar'

    implementation project(path: ':orderednetworkview')
    implementation 'com.erhannis.android:ekandroid:0.1.0-SNAPSHOT@aar'
    implementation ('com.erhannis.android:distributed-ui:1.0@aar') {
        transitive = true
    }

    testImplementation 'junit:junit:4.12'
}
